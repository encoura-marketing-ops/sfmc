%%[
    /* Declare variables */
    VAR @campaignID, @campaignName, @rows, @row, @firstName, @lastName, @title, @companyOrAccount, @email, @status
    VAR @campaignMemberID, @action, @statusMessage, @updateResponse, @urlCampaignID
    VAR @message /* Variable for the dynamic message */

    /* Retrieve form parameters */
    SET @action = RequestParameter("action")
    SET @campaignMemberID = RequestParameter("campaignMemberID")
    SET @campaignID = RequestParameter("campaignID")

    /* Retrieve CampaignID from URL if present */
    SET @urlCampaignID = QueryParameter("campaignID")

IF EMPTY(@urlCampaignID) THEN
    SET @displayCampaignSearch = 'inline'
    SET @displayCampaignMemberSearch = 'none'
ELSE 
    SET @displayCampaignSearch = 'none'
    SET @displayCampaignMemberSearch = 'inline'
ENDIF
    
    /* Use the CampaignID from the URL if it exists */
    IF NOT EMPTY(@urlCampaignID) THEN
        SET @campaignID = @urlCampaignID
    ENDIF

    /* Initialize status message */
    SET @statusMessage = ""

    /* Initialize the message variable */
    IF EMPTY(@campaignID) THEN
        SET @message = "Enter a CampaignID to get started."
    ELSE
        /* Retrieve the campaign name */
        SET @campaignRow = RetrieveSalesforceObjects(
            "Campaign", 
            "Name", 
            "Id", "=", @campaignID
        )
        
        IF RowCount(@campaignRow) > 0 THEN
            SET @campaignName = Field(Row(@campaignRow, 1), "Name")
        ELSE
            SET @campaignName = ""
        ENDIF

        /* Retrieve campaign members if a CampaignID is set */
        SET @rows = RetrieveSalesforceObjects(
            "CampaignMember", 
            "Id, FirstName, LastName, Title, CompanyOrAccount, Email, Status", 
            "CampaignId", "=", @campaignID
        )

        SET @filteredRows = CreateObject("Rows")
        SET @rowCount = RowCount(@rows)

        /* First: filter into @filteredRows */
        FOR @i = 1 TO @rowCount DO
          SET @row = Row(@rows, @i)
          SET @status = Field(@row, "Status")

          IF @status == "Registered" OR @status == "Attended" OR @status == "Waitlisted" THEN
            AddObjectArrayItem(@filteredRows, @row)
          ENDIF
        NEXT


        /* Check if there are no campaign members */
        IF RowCount(@rows) == 0 THEN
            SET @message = "No campaign members found."
        ELSE
            SET @message = "" /* Clear message if members are found */
        ENDIF
    ENDIF

    /* Check if form was submitted to update status */
    IF @action == "updateStatus" AND NOT EMPTY(@campaignMemberID) THEN
        IF RequestParameter("newStatus") == "Attended" THEN
            SET @updateResponse = UpdateSingleSalesforceObject(
                "CampaignMember", 
                @campaignMemberID, 
                "Status", "Attended"
            )
        ELSEIF RequestParameter("newStatus") == "Registered" THEN
            SET @updateResponse = UpdateSingleSalesforceObject(
                "CampaignMember", 
                @campaignMemberID, 
                "Status", "Registered"
            )
        ENDIF
        
        /* Log the update response for debugging */
        SET @statusMessage = CONCAT("Status update response: ", @updateResponse)
        SET @displayStatusMessage = 'none'
    ENDIF
]%%

<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Check-In App</title>
    <link rel="stylesheet" href="https://cloud.mail.encoura.org/style.css">  
    <link rel="stylesheet" href="https://cloud.mail.encoura.org/check-in-style.css">  
  <link rel="icon" href="https://www.encoura.org/favicon.ico"/><link rel="apple-touch-icon" sizes="60x60" href="https://www.encoura.org/apple-touch-icon.png"/><link rel="icon" type="image/png" sizes="32x32" href="https://www.encoura.org/favicon-32x32.png"/><link rel="icon" type="image/png" sizes="16x16" href="https://www.encoura.org/favicon-16x16.png"/><link rel="mask-icon" href="https://www.encoura.org/safari-pinned-tab.svg" color="#5bbad5"/><meta name="msapplication-TileColor" content="#da532c"/>
    <script src="https://cloud.mail.encoura.org/sort-table-check-in.js" type="text/javascript"></script>
</head>
<body>
  <div class="container">
   
  
    <h1>Check In App</h1>
    <h2>%%=v(@campaignName)=%%</h2>
    <div class="toolbar">
      
    
    <!-- Form to enter CampaignID -->
    <form style="display: %%=v(@displayCampaignSearch)=%%;" class="campaignSearch" method="get" action="%%=RequestParameter('PAGEURL')=%%">
        <label for="campaignID">Enter Campaign ID:</label>
        <input type="text" id="campaignID" name="campaignID" value="%%=v(@campaignID)=%%" required>
        <input type="submit" value="Load Campaign">
    </form>

    <!-- Search box for filtering the table -->
    <form style="display: %%=v(@displayCampaignMemberSearch)=%%;" class="campaignMemberSearch" > 
      <label for="searchInput">Search Registrants:</label>
      <input type="text" id="searchInput" onkeyup="filterTable()" placeholder="Start typing...">
      </form>
      <p id="searchCount">Showing all results</p>
      <p id="checkedInCount">0 out of 0 checked in</p>
</div>
    %%[
        /* Display status message if applicable */
        IF @statusMessage != "" THEN
    ]%%
    <p style="display: %%=v(@displayStatusMessage)=%%;">%%=v(@statusMessage)=%%</p>
    %%[
        ENDIF
    ]%%

    %%[
        /* Check if there are any rows */
        IF NOT EMPTY(@campaignID) AND RowCount(@rows) > 0 THEN
    ]%%
    <table id="registrantsTable">
        <thead>
            <tr>
                <th onclick="sortTable(0)">First Name</th>
                <th onclick="sortTable(1)">Last Name</th>
                <th onclick="sortTable(2)">Title</th>
                <th onclick="sortTable(3)">Company or Account</th>
                <th onclick="sortTable(4)">Email</th>
                <th onclick="sortTable(5)">Status</th>
                <th>Action</th>
            </tr>
        </thead>
        <tbody>
            %%[
                FOR @i = 1 TO RowCount(@filteredRows) DO
                    SET @row = Row(@filteredRows, @i)
                    SET @firstName = Field(@row, "FirstName")
                    SET @lastName = Field(@row, "LastName")
                    SET @title = Field(@row, "Title")
                    SET @companyOrAccount = Field(@row, "CompanyOrAccount")
                    SET @email = Field(@row, "Email")
                    SET @status = Field(@row, "Status")
                    SET @campaignMemberID = Field(@row, "Id")
            ]%%
            <tr>
                <td>%%=v(@firstName)=%%</td>
                <td>%%=v(@lastName)=%%</td>
                <td>%%=v(@title)=%%</td>
                <td>%%=v(@companyOrAccount)=%%</td>
                <td>%%=v(@email)=%%</td>
                <td>%%=v(@status)=%%</td>
                <td>
                    %%[
                        IF @status == "Attended" THEN
                    ]%%
                    <form action="%%=RequestParameter('PAGEURL')=%%" method="post">
                        <input type="hidden" name="campaignMemberID" value="%%=v(@campaignMemberID)=%%" />
                        <input type="hidden" name="action" value="updateStatus" />
                        <input type="hidden" name="newStatus" value="Registered" />
                        <input type="hidden" name="campaignID" value="%%=v(@campaignID)=%%" />
                        <input type="submit" class="action-button check-out-button" value="Check Out" />
                    </form>
                    %%[
                        ELSE
                    ]%%
                    <form action="%%=RequestParameter('PAGEURL')=%%" method="post">
                        <input type="hidden" name="campaignMemberID" value="%%=v(@campaignMemberID)=%%" />
                        <input type="hidden" name="action" value="updateStatus" />
                        <input type="hidden" name="newStatus" value="Attended" />
                        <input type="hidden" name="campaignID" value="%%=v(@campaignID)=%%" />
                        <input type="submit" class="action-button check-in-button" value="Check In" />
                    </form>
                    %%[
                        ENDIF
                    ]%%
                </td>
            </tr>
            %%[
                NEXT
            ]%%
        </tbody>
    </table>
    %%[
        ELSE
    ]%%

    %%[
        ENDIF
    ]%%
  </div>
     <!-- Footer -->
%%=ContentBlockByID('361559')=%%
</body>
</html>
